
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html>
<head>
<title>PERLDSC</title><meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
<link rel="stylesheet" type="text/css" href="/global/main.css" title="default">
</head>

<body>



<div id="container">


<div id="content">

<div id='catHeader'><table width='100%'><tr><td>
<H1>PERLDSC</H1>
Section: Perl Programmers Reference Guide (1)<BR>Updated: 2004-04-23<BR>
</td><td align='right' valign='bottom'><div class='ad_header_right'></div></td></tr></table></div></div>
<div id='categories'>
<div class='ad_man_right'>
</div>

<A NAME="lbAB">&nbsp;</A>
<H2>NAME</H2>

perldsc - Perl Data Structures Cookbook
<A NAME="lbAC">&nbsp;</A>
<H2>DESCRIPTION</H2>

<A NAME="ixAAC"></A>
The single feature most sorely lacking in the Perl programming language
prior to its 5.0 release was complex data structures.  Even without direct
language support, some valiant programmers did manage to emulate them, but
it was hard work and not for the faint of heart.  You could occasionally
get away with the <TT>$m{$AoA,$b}</TT> notation borrowed from <B>awk</B> in which the
keys are actually more like a single concatenated string <TT>&quot;$AoA$b&quot;</TT>, but
traversal and sorting were difficult.  More desperate programmers even
hacked Perl's internal symbol table directly, a strategy that proved hard
to develop and maintain---to put it mildly.
<P>

The 5.0 release of Perl let us have complex data structures.  You
may now write something like this and all of a sudden, you'd have an array
with three dimensions!
<P>



<PRE>
    for $x (1 .. 10) {
        for $y (1 .. 10) {
            for $z (1 .. 10) {
                $AoA[$x][$y][$z] =
                    $x ** $y + $z;
            }
        }
    }

</PRE>


<P>

Alas, however simple this may appear, underneath it's a much more
elaborate construct than meets the eye!
<P>

How do you print it out?  Why can't you say just <TT>&quot;print @AoA&quot;</TT>?  How do
you sort it?  How can you pass it to a function or get one of these back
from a function?  Is it an object?  Can you save it to disk to read
back later?  How do you access whole rows or columns of that matrix?  Do
all the values have to be numeric?
<P>

As you see, it's quite easy to become confused.  While some small portion
of the blame for this can be attributed to the reference-based
implementation, it's really more due to a lack of existing documentation with
examples designed for the beginner.
<P>

This document is meant to be a detailed but understandable treatment of the
many different sorts of data structures you might want to develop.  It
should also serve as a cookbook of examples.  That way, when you need to
create one of these complex data structures, you can just pinch, pilfer, or
purloin a drop-in example from here.
<P>

Let's look at each of these possible constructs in detail.  There are separate
sections on each of the following:
<DL COMPACT>
<DT>* arrays of arrays<DD>
<A NAME="ixAAD"></A>

<DT>* hashes of arrays<DD>
<A NAME="ixAAE"></A>
<DT>* arrays of hashes<DD>
<A NAME="ixAAF"></A>
<DT>* hashes of hashes<DD>
<A NAME="ixAAG"></A>
<DT>* more elaborate constructs<DD>
<A NAME="ixAAH"></A>

</DL>
<P>

But for now, let's look at general issues common to all
these types of data structures.
<A NAME="lbAD">&nbsp;</A>
<H2>REFERENCES</H2>

<A NAME="ixAAI"></A>
The most important thing to understand about all data structures in Perl
--- including multidimensional arrays---is that even though they might
appear otherwise, Perl <TT>@ARRAY</TT>s and <TT>%HASH</TT>es are all internally
one-dimensional.  They can hold only scalar values (meaning a string,
number, or a reference).  They cannot directly contain other arrays or
hashes, but instead contain <I>references</I> to other arrays or hashes.
<P>

You can't use a reference to an array or hash in quite the same way that you
would a real array or hash.  For C or C<FONT SIZE="-2">++</FONT> programmers unused to
distinguishing between arrays and pointers to the same, this can be
confusing.  If so, just think of it as the difference between a structure
and a pointer to a structure.
<P>

You can (and should) read more about references in the <I><A HREF="../man1/perlref.1.php">perlref</A></I>(1) man
page.  Briefly, references are rather like pointers that know what they
point to.  (Objects are also a kind of reference, but we won't be needing
them right away---if ever.)  This means that when you have something which
looks to you like an access to a two-or-more-dimensional array and/or hash,
what's really going on is that the base type is
merely a one-dimensional entity that contains references to the next
level.  It's just that you can <I>use</I> it as though it were a
two-dimensional one.  This is actually the way almost all C
multidimensional arrays work as well.
<P>



<PRE>
    $array[7][12]                       # array of arrays
    $array[7]{string}                   # array of hashes
    $hash{string}[7]                    # hash of arrays
    $hash{string}{'another string'}     # hash of hashes

</PRE>


<P>

Now, because the top level contains only references, if you try to print
out your array in with a simple <I>print()</I> function, you'll get something
that doesn't look very nice, like this:
<P>



<PRE>
    @AoA = ( [2, 3], [4, 5, 7], [0] );
    print $AoA[1][2];
  7
    print @AoA;
  ARRAY(0x83c38)ARRAY(0x8b194)ARRAY(0x8b1d0)

</PRE>


<P>

That's because Perl doesn't (ever) implicitly dereference your variables.
If you want to get at the thing a reference is referring to, then you have
to do this yourself using either prefix typing indicators, like
<TT>&quot;${$blah}&quot;</TT>, <TT>&quot;@{$blah}&quot;</TT>, <TT>&quot;@{$blah[$i]}&quot;</TT>, or else postfix pointer arrows,
like <TT>&quot;$a-&gt;[3]&quot;</TT>, <TT>&quot;$h-&gt;{fred}&quot;</TT>, or even <TT>&quot;$ob-&gt;method()-&gt;[3]&quot;</TT>.
<A NAME="lbAE">&nbsp;</A>
<H2>COMMON MISTAKES</H2>

<A NAME="ixAAJ"></A>
The two most common mistakes made in constructing something like
an array of arrays is either accidentally counting the number of
elements or else taking a reference to the same memory location
repeatedly.  Here's the case where you just get the count instead
of a nested array:
<P>



<PRE>
    for $i (1..10) {
        @array = somefunc($i);
        $AoA[$i] = @array;      # WRONG!
    }

</PRE>


<P>

That's just the simple case of assigning an array to a scalar and getting
its element count.  If that's what you really and truly want, then you
might do well to consider being a tad more explicit about it, like this:
<P>



<PRE>
    for $i (1..10) {
        @array = somefunc($i);
        $counts[$i] = scalar @array;
    }

</PRE>


<P>

Here's the case of taking a reference to the same memory location
again and again:
<P>



<PRE>
    for $i (1..10) {
        @array = somefunc($i);
        $AoA[$i] = \@array;     # WRONG!
    }

</PRE>


<P>

So, what's the big problem with that?  It looks right, doesn't it?
After all, I just told you that you need an array of references, so by
golly, you've made me one!
<P>

Unfortunately, while this is true, it's still broken.  All the references
in <TT>@AoA</TT> refer to the <I>very same place</I>, and they will therefore all hold
whatever was last in <TT>@array</TT>!  It's similar to the problem demonstrated in
the following C program:
<P>



<PRE>
    #include &lt;<A HREF="file:/usr/include/pwd.h">pwd.h</A>&gt;
    main() {
        struct passwd *getpwnam(), *rp, *dp;
        rp = getpwnam(&quot;root&quot;);
        dp = getpwnam(&quot;daemon&quot;);

</PRE>


<P>



<PRE>
        printf(&quot;daemon name is %s\nroot name is %s\n&quot;,
                dp-&gt;pw_name, rp-&gt;pw_name);
    }

</PRE>


<P>

Which will print
<P>



<PRE>
    daemon name is daemon
    root name is daemon

</PRE>


<P>

The problem is that both <TT>&quot;rp&quot;</TT> and <TT>&quot;dp&quot;</TT> are pointers to the same location
in memory!  In C, you'd have to remember to <I>malloc()</I> yourself some new
memory.  In Perl, you'll want to use the array constructor <TT>&quot;[]&quot;</TT> or the
hash constructor <TT>&quot;{}&quot;</TT> instead.   Here's the right way to do the preceding
broken code fragments:
<P>



<PRE>
    for $i (1..10) {
        @array = somefunc($i);
        $AoA[$i] = [ @array ];
    }

</PRE>


<P>

The square brackets make a reference to a new array with a <I>copy</I>
of what's in <TT>@array</TT> at the time of the assignment.  This is what
you want.
<P>

Note that this will produce something similar, but it's
much harder to read:
<P>



<PRE>
    for $i (1..10) {
        @array = 0 .. $i;
        @{$AoA[$i]} = @array;
    }

</PRE>


<P>

Is it the same?  Well, maybe so---and maybe not.  The subtle difference
is that when you assign something in square brackets, you know for sure
it's always a brand new reference with a new <I>copy</I> of the data.
Something else could be going on in this new case with the <TT>&quot;@{$AoA[$i]}}&quot;</TT>
dereference on the left-hand-side of the assignment.  It all depends on
whether <TT>$AoA[$i]</TT> had been undefined to start with, or whether it
already contained a reference.  If you had already populated <TT>@AoA</TT> with
references, as in
<P>



<PRE>
    $AoA[3] = \@another_array;

</PRE>


<P>

Then the assignment with the indirection on the left-hand-side would
use the existing reference that was already there:
<P>



<PRE>
    @{$AoA[3]} = @array;

</PRE>


<P>

Of course, this <I>would</I> have the ``interesting'' effect of clobbering
<TT>@another_array</TT>.  (Have you ever noticed how when a programmer says
something is ``interesting'', that rather than meaning ``intriguing'',
they're disturbingly more apt to mean that it's ``annoying'',
``difficult'', or both?  :-)
<P>

So just remember always to use the array or hash constructors with <TT>&quot;[]&quot;</TT>
or <TT>&quot;{}&quot;</TT>, and you'll be fine, although it's not always optimally
efficient.
<P>

Surprisingly, the following dangerous-looking construct will
actually work out fine:
<P>



<PRE>
    for $i (1..10) {
        my @array = somefunc($i);
        $AoA[$i] = \@array;
    }

</PRE>


<P>

That's because <I>my()</I> is more of a run-time statement than it is a
compile-time declaration <I>per se</I>.  This means that the <I>my()</I> variable is
remade afresh each time through the loop.  So even though it <I>looks</I> as
though you stored the same variable reference each time, you actually did
not!  This is a subtle distinction that can produce more efficient code at
the risk of misleading all but the most experienced of programmers.  So I
usually advise against teaching it to beginners.  In fact, except for
passing arguments to functions, I seldom like to see the gimme-a-reference
operator (backslash) used much at all in code.  Instead, I advise
beginners that they (and most of the rest of us) should try to use the
much more easily understood constructors <TT>&quot;[]&quot;</TT> and <TT>&quot;{}&quot;</TT> instead of
relying upon lexical (or dynamic) scoping and hidden reference-counting to
do the right thing behind the scenes.
<P>

In summary:
<P>



<PRE>
    $AoA[$i] = [ @array ];      # usually best
    $AoA[$i] = \@array;         # perilous; just how my() was that array?
    @{ $AoA[$i] } = @array;     # way too tricky for most programmers

</PRE>


<A NAME="lbAF">&nbsp;</A>
<H2>CAVEAT ON PRECEDENCE</H2>

<A NAME="ixAAK"></A>
Speaking of things like <TT>&quot;@{$AoA[$i]}&quot;</TT>, the following are actually the
same thing:
<P>



<PRE>
    $aref-&gt;[2][2]       # clear
    $$aref[2][2]        # confusing

</PRE>


<P>

That's because Perl's precedence rules on its five prefix dereferencers
(which look like someone swearing: <TT>&quot;$ @ * % &amp;&quot;</TT>) make them bind more
tightly than the postfix subscripting brackets or braces!  This will no
doubt come as a great shock to the C or C<FONT SIZE="-2">++</FONT> programmer, who is quite
accustomed to using <TT>*a[i]</TT> to mean what's pointed to by the <I>i'th</I>
element of <TT>&quot;a&quot;</TT>.  That is, they first take the subscript, and only then
dereference the thing at that subscript.  That's fine in C, but this isn't C.
<P>

The seemingly equivalent construct in Perl, <TT>$$aref[$i]</TT> first does
the deref of <TT>$aref</TT>, making it take <TT>$aref</TT> as a reference to an
array, and then dereference that, and finally tell you the <I>i'th</I> value
of the array pointed to by <TT>$AoA</TT>. If you wanted the C notion, you'd have to
write <TT>&quot;${$AoA[$i]}&quot;</TT> to force the <TT>$AoA[$i]</TT> to get evaluated first
before the leading <TT>&quot;$&quot;</TT> dereferencer.
<A NAME="lbAG">&nbsp;</A>
<H2>WHY YOU SHOULD ALWAYS use strict</H2>



<A NAME="ixAAL"></A>
If this is starting to sound scarier than it's worth, relax.  Perl has
some features to help you avoid its most common pitfalls.  The best
way to avoid getting confused is to start every program like this:
<P>



<PRE>
    #!/usr/bin/perl -w
    use strict;

</PRE>


<P>

This way, you'll be forced to declare all your variables with <I>my()</I> and
also disallow accidental ``symbolic dereferencing''.  Therefore if you'd done
this:
<P>



<PRE>
    my $aref = [
        [ &quot;fred&quot;, &quot;barney&quot;, &quot;pebbles&quot;, &quot;bambam&quot;, &quot;dino&quot;, ],
        [ &quot;homer&quot;, &quot;bart&quot;, &quot;marge&quot;, &quot;maggie&quot;, ],
        [ &quot;george&quot;, &quot;jane&quot;, &quot;elroy&quot;, &quot;judy&quot;, ],
    ];

</PRE>


<P>



<PRE>
    print $aref[2][2];

</PRE>


<P>

The compiler would immediately flag that as an error <I>at compile time</I>,
because you were accidentally accessing <TT>@aref</TT>, an undeclared
variable, and it would thereby remind you to write instead:
<P>



<PRE>
    print $aref-&gt;[2][2]

</PRE>


<A NAME="lbAH">&nbsp;</A>
<H2>DEBUGGING</H2>

<A NAME="ixAAM"></A>
Before version 5.002, the standard Perl debugger didn't do a very nice job of
printing out complex data structures.  With 5.002 or above, the
debugger includes several new features, including command line editing as
well as the <TT>&quot;x&quot;</TT> command to dump out complex data structures.  For
example, given the assignment to <TT>$AoA</TT> above, here's the debugger output:
<P>



<PRE>
    DB&lt;1&gt; x $AoA
    $AoA = ARRAY(0x13b5a0)
       0  ARRAY(0x1f0a24)
          0  'fred'
          1  'barney'
          2  'pebbles'
          3  'bambam'
          4  'dino'
       1  ARRAY(0x13b558)
          0  'homer'
          1  'bart'
          2  'marge'
          3  'maggie'
       2  ARRAY(0x13b540)
          0  'george'
          1  'jane'
          2  'elroy'
          3  'judy'

</PRE>


<A NAME="lbAI">&nbsp;</A>
<H2>CODE EXAMPLES</H2>

<A NAME="ixAAN"></A>
Presented with little comment (these will get their own manpages someday)
here are short code examples illustrating access of various
types of data structures.
<A NAME="lbAJ">&nbsp;</A>
<H2>ARRAYS OF ARRAYS</H2>

<A NAME="ixAAO"></A>
<A NAME="lbAK">&nbsp;</A>
<H2>Declaration of an <FONT SIZE="-1">ARRAY</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">ARRAYS</FONT></H2>

<A NAME="ixAAP"></A>


<PRE>
 @AoA = (
        [ &quot;fred&quot;, &quot;barney&quot; ],
        [ &quot;george&quot;, &quot;jane&quot;, &quot;elroy&quot; ],
        [ &quot;homer&quot;, &quot;marge&quot;, &quot;bart&quot; ],
      );

</PRE>


<A NAME="lbAL">&nbsp;</A>
<H2>Generation of an <FONT SIZE="-1">ARRAY</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">ARRAYS</FONT></H2>

<A NAME="ixAAQ"></A>


<PRE>
 # reading from file
 while ( &lt;&gt; ) {
     push @AoA, [ split ];
 }

</PRE>


<P>



<PRE>
 # calling a function
 for $i ( 1 .. 10 ) {
     $AoA[$i] = [ somefunc($i) ];
 }

</PRE>


<P>



<PRE>
 # using temp vars
 for $i ( 1 .. 10 ) {
     @tmp = somefunc($i);
     $AoA[$i] = [ @tmp ];
 }

</PRE>


<P>



<PRE>
 # add to an existing row
 push @{ $AoA[0] }, &quot;wilma&quot;, &quot;betty&quot;;

</PRE>


<A NAME="lbAM">&nbsp;</A>
<H2>Access and Printing of an <FONT SIZE="-1">ARRAY</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">ARRAYS</FONT></H2>

<A NAME="ixAAR"></A>


<PRE>
 # one element
 $AoA[0][0] = &quot;Fred&quot;;

</PRE>


<P>



<PRE>
 # another element
 $AoA[1][1] =~ s/(\w)/\u$1/;

</PRE>


<P>



<PRE>
 # print the whole thing with refs
 for $aref ( @AoA ) {
     print &quot;\t [ @$aref ],\n&quot;;
 }

</PRE>


<P>



<PRE>
 # print the whole thing with indices
 for $i ( 0 .. $#AoA ) {
     print &quot;\t [ @{$AoA[$i]} ],\n&quot;;
 }

</PRE>


<P>



<PRE>
 # print the whole thing one at a time
 for $i ( 0 .. $#AoA ) {
     for $j ( 0 .. $#{ $AoA[$i] } ) {
         print &quot;elt $i $j is $AoA[$i][$j]\n&quot;;
     }
 }

</PRE>


<A NAME="lbAN">&nbsp;</A>
<H2>HASHES OF ARRAYS</H2>

<A NAME="ixAAS"></A>
<A NAME="lbAO">&nbsp;</A>
<H2>Declaration of a <FONT SIZE="-1">HASH</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">ARRAYS</FONT></H2>

<A NAME="ixAAT"></A>


<PRE>
 %HoA = (
        flintstones        =&gt; [ &quot;fred&quot;, &quot;barney&quot; ],
        jetsons            =&gt; [ &quot;george&quot;, &quot;jane&quot;, &quot;elroy&quot; ],
        simpsons           =&gt; [ &quot;homer&quot;, &quot;marge&quot;, &quot;bart&quot; ],
      );

</PRE>


<A NAME="lbAP">&nbsp;</A>
<H2>Generation of a <FONT SIZE="-1">HASH</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">ARRAYS</FONT></H2>

<A NAME="ixAAU"></A>


<PRE>
 # reading from file
 # flintstones: fred barney wilma dino
 while ( &lt;&gt; ) {
     next unless s/^(.*?):\s*//;
     $HoA{$1} = [ split ];
 }

</PRE>


<P>



<PRE>
 # reading from file; more temps
 # flintstones: fred barney wilma dino
 while ( $line = &lt;&gt; ) {
     ($who, $rest) = split /:\s*/, $line, 2;
     @fields = split ' ', $rest;
     $HoA{$who} = [ @fields ];
 }

</PRE>


<P>



<PRE>
 # calling a function that returns a list
 for $group ( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; ) {
     $HoA{$group} = [ get_family($group) ];
 }

</PRE>


<P>



<PRE>
 # likewise, but using temps
 for $group ( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; ) {
     @members = get_family($group);
     $HoA{$group} = [ @members ];
 }

</PRE>


<P>



<PRE>
 # append new members to an existing family
 push @{ $HoA{&quot;flintstones&quot;} }, &quot;wilma&quot;, &quot;betty&quot;;

</PRE>


<A NAME="lbAQ">&nbsp;</A>
<H2>Access and Printing of a <FONT SIZE="-1">HASH</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">ARRAYS</FONT></H2>

<A NAME="ixAAV"></A>


<PRE>
 # one element
 $HoA{flintstones}[0] = &quot;Fred&quot;;

</PRE>


<P>



<PRE>
 # another element
 $HoA{simpsons}[1] =~ s/(\w)/\u$1/;

</PRE>


<P>



<PRE>
 # print the whole thing
 foreach $family ( keys %HoA ) {
     print &quot;$family: @{ $HoA{$family} }\n&quot;
 }

</PRE>


<P>



<PRE>
 # print the whole thing with indices
 foreach $family ( keys %HoA ) {
     print &quot;family: &quot;;
     foreach $i ( 0 .. $#{ $HoA{$family} } ) {
         print &quot; $i = $HoA{$family}[$i]&quot;;
     }
     print &quot;\n&quot;;
 }

</PRE>


<P>



<PRE>
 # print the whole thing sorted by number of members
 foreach $family ( sort { @{$HoA{$b}} &lt;=&gt; @{$HoA{$a}} } keys %HoA ) {
     print &quot;$family: @{ $HoA{$family} }\n&quot;
 }

</PRE>


<P>



<PRE>
 # print the whole thing sorted by number of members and name
 foreach $family ( sort {
                            @{$HoA{$b}} &lt;=&gt; @{$HoA{$a}}
                                        ||
                                    $a cmp $b
            } keys %HoA )
 {
     print &quot;$family: &quot;, join(&quot;, &quot;, sort @{ $HoA{$family} }), &quot;\n&quot;;
 }

</PRE>


<A NAME="lbAR">&nbsp;</A>
<H2>ARRAYS OF HASHES</H2>

<A NAME="ixAAW"></A>
<A NAME="lbAS">&nbsp;</A>
<H2>Declaration of an <FONT SIZE="-1">ARRAY</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">HASHES</FONT></H2>

<A NAME="ixAAX"></A>


<PRE>
 @AoH = (
        {
            Lead     =&gt; &quot;fred&quot;,
            Friend   =&gt; &quot;barney&quot;,
        },
        {
            Lead     =&gt; &quot;george&quot;,
            Wife     =&gt; &quot;jane&quot;,
            Son      =&gt; &quot;elroy&quot;,
        },
        {
            Lead     =&gt; &quot;homer&quot;,
            Wife     =&gt; &quot;marge&quot;,
            Son      =&gt; &quot;bart&quot;,
        }
  );

</PRE>


<A NAME="lbAT">&nbsp;</A>
<H2>Generation of an <FONT SIZE="-1">ARRAY</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">HASHES</FONT></H2>

<A NAME="ixAAY"></A>


<PRE>
 # reading from file
 # format: LEAD=fred FRIEND=barney
 while ( &lt;&gt; ) {
     $rec = {};
     for $field ( split ) {
         ($key, $value) = split /=/, $field;
         $rec-&gt;{$key} = $value;
     }
     push @AoH, $rec;
 }

</PRE>


<P>



<PRE>
 # reading from file
 # format: LEAD=fred FRIEND=barney
 # no temp
 while ( &lt;&gt; ) {
     push @AoH, { split /[\s+=]/ };
 }

</PRE>


<P>



<PRE>
 # calling a function  that returns a key/value pair list, like
 # &quot;lead&quot;,&quot;fred&quot;,&quot;daughter&quot;,&quot;pebbles&quot;
 while ( %fields = getnextpairset() ) {
     push @AoH, { %fields };
 }

</PRE>


<P>



<PRE>
 # likewise, but using no temp vars
 while (&lt;&gt;) {
     push @AoH, { parsepairs($_) };
 }

</PRE>


<P>



<PRE>
 # add key/value to an element
 $AoH[0]{pet} = &quot;dino&quot;;
 $AoH[2]{pet} = &quot;santa's little helper&quot;;

</PRE>


<A NAME="lbAU">&nbsp;</A>
<H2>Access and Printing of an <FONT SIZE="-1">ARRAY</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">HASHES</FONT></H2>

<A NAME="ixAAZ"></A>


<PRE>
 # one element
 $AoH[0]{lead} = &quot;fred&quot;;

</PRE>


<P>



<PRE>
 # another element
 $AoH[1]{lead} =~ s/(\w)/\u$1/;

</PRE>


<P>



<PRE>
 # print the whole thing with refs
 for $href ( @AoH ) {
     print &quot;{ &quot;;
     for $role ( keys %$href ) {
         print &quot;$role=$href-&gt;{$role} &quot;;
     }
     print &quot;}\n&quot;;
 }

</PRE>


<P>



<PRE>
 # print the whole thing with indices
 for $i ( 0 .. $#AoH ) {
     print &quot;$i is { &quot;;
     for $role ( keys %{ $AoH[$i] } ) {
         print &quot;$role=$AoH[$i]{$role} &quot;;
     }
     print &quot;}\n&quot;;
 }

</PRE>


<P>



<PRE>
 # print the whole thing one at a time
 for $i ( 0 .. $#AoH ) {
     for $role ( keys %{ $AoH[$i] } ) {
         print &quot;elt $i $role is $AoH[$i]{$role}\n&quot;;
     }
 }

</PRE>


<A NAME="lbAV">&nbsp;</A>
<H2>HASHES OF HASHES</H2>

<A NAME="ixABA"></A>
<A NAME="lbAW">&nbsp;</A>
<H2>Declaration of a <FONT SIZE="-1">HASH</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">HASHES</FONT></H2>

<A NAME="ixABB"></A>


<PRE>
 %HoH = (
        flintstones =&gt; {
                lead      =&gt; &quot;fred&quot;,
                pal       =&gt; &quot;barney&quot;,
        },
        jetsons     =&gt; {
                lead      =&gt; &quot;george&quot;,
                wife      =&gt; &quot;jane&quot;,
                &quot;his boy&quot; =&gt; &quot;elroy&quot;,
        },
        simpsons    =&gt; {
                lead      =&gt; &quot;homer&quot;,
                wife      =&gt; &quot;marge&quot;,
                kid       =&gt; &quot;bart&quot;,
        },
 );

</PRE>


<A NAME="lbAX">&nbsp;</A>
<H2>Generation of a <FONT SIZE="-1">HASH</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">HASHES</FONT></H2>

<A NAME="ixABC"></A>


<PRE>
 # reading from file
 # flintstones: lead=fred pal=barney wife=wilma pet=dino
 while ( &lt;&gt; ) {
     next unless s/^(.*?):\s*//;
     $who = $1;
     for $field ( split ) {
         ($key, $value) = split /=/, $field;
         $HoH{$who}{$key} = $value;
     }

</PRE>


<P>



<PRE>
 # reading from file; more temps
 while ( &lt;&gt; ) {
     next unless s/^(.*?):\s*//;
     $who = $1;
     $rec = {};
     $HoH{$who} = $rec;
     for $field ( split ) {
         ($key, $value) = split /=/, $field;
         $rec-&gt;{$key} = $value;
     }
 }

</PRE>


<P>



<PRE>
 # calling a function  that returns a key,value hash
 for $group ( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; ) {
     $HoH{$group} = { get_family($group) };
 }

</PRE>


<P>



<PRE>
 # likewise, but using temps
 for $group ( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; ) {
     %members = get_family($group);
     $HoH{$group} = { %members };
 }

</PRE>


<P>



<PRE>
 # append new members to an existing family
 %new_folks = (
     wife =&gt; &quot;wilma&quot;,
     pet  =&gt; &quot;dino&quot;,
 );

</PRE>


<P>



<PRE>
 for $what (keys %new_folks) {
     $HoH{flintstones}{$what} = $new_folks{$what};
 }

</PRE>


<A NAME="lbAY">&nbsp;</A>
<H2>Access and Printing of a <FONT SIZE="-1">HASH</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">HASHES</FONT></H2>

<A NAME="ixABD"></A>


<PRE>
 # one element
 $HoH{flintstones}{wife} = &quot;wilma&quot;;

</PRE>


<P>



<PRE>
 # another element
 $HoH{simpsons}{lead} =~ s/(\w)/\u$1/;

</PRE>


<P>



<PRE>
 # print the whole thing
 foreach $family ( keys %HoH ) {
     print &quot;$family: { &quot;;
     for $role ( keys %{ $HoH{$family} } ) {
         print &quot;$role=$HoH{$family}{$role} &quot;;
     }
     print &quot;}\n&quot;;
 }

</PRE>


<P>



<PRE>
 # print the whole thing  somewhat sorted
 foreach $family ( sort keys %HoH ) {
     print &quot;$family: { &quot;;
     for $role ( sort keys %{ $HoH{$family} } ) {
         print &quot;$role=$HoH{$family}{$role} &quot;;
     }
     print &quot;}\n&quot;;
 }

</PRE>


<P>



<PRE>
 # print the whole thing sorted by number of members
 foreach $family ( sort { keys %{$HoH{$b}} &lt;=&gt; keys %{$HoH{$a}} } keys %HoH ) {
     print &quot;$family: { &quot;;
     for $role ( sort keys %{ $HoH{$family} } ) {
         print &quot;$role=$HoH{$family}{$role} &quot;;
     }
     print &quot;}\n&quot;;
 }

</PRE>


<P>



<PRE>
 # establish a sort order (rank) for each role
 $i = 0;
 for ( qw(lead wife son daughter pal pet) ) { $rank{$_} = ++$i }

</PRE>


<P>



<PRE>
 # now print the whole thing sorted by number of members
 foreach $family ( sort { keys %{ $HoH{$b} } &lt;=&gt; keys %{ $HoH{$a} } } keys %HoH ) {
     print &quot;$family: { &quot;;
     # and print these according to rank order
     for $role ( sort { $rank{$a} &lt;=&gt; $rank{$b} }  keys %{ $HoH{$family} } ) {
         print &quot;$role=$HoH{$family}{$role} &quot;;
     }
     print &quot;}\n&quot;;
 }

</PRE>


<A NAME="lbAZ">&nbsp;</A>
<H2>MORE ELABORATE RECORDS</H2>

<A NAME="ixABE"></A>
<A NAME="lbBA">&nbsp;</A>
<H2>Declaration of <FONT SIZE="-1">MORE</FONT> <FONT SIZE="-1">ELABORATE</FONT> <FONT SIZE="-1">RECORDS</FONT></H2>

<A NAME="ixABF"></A>
Here's a sample showing how to create and use a record whose fields are of
many different sorts:
<P>



<PRE>
     $rec = {
         TEXT      =&gt; $string,
         SEQUENCE  =&gt; [ @old_values ],
         LOOKUP    =&gt; { %some_table },
         THATCODE  =&gt; \&amp;some_function,
         THISCODE  =&gt; sub { $_[0] ** $_[1] },
         HANDLE    =&gt; \*STDOUT,
     };

</PRE>


<P>



<PRE>
     print $rec-&gt;{TEXT};

</PRE>


<P>



<PRE>
     print $rec-&gt;{SEQUENCE}[0];
     $last = pop @ { $rec-&gt;{SEQUENCE} };

</PRE>


<P>



<PRE>
     print $rec-&gt;{LOOKUP}{&quot;key&quot;};
     ($first_k, $first_v) = each %{ $rec-&gt;{LOOKUP} };

</PRE>


<P>



<PRE>
     $answer = $rec-&gt;{THATCODE}-&gt;($arg);
     $answer = $rec-&gt;{THISCODE}-&gt;($arg1, $arg2);

</PRE>


<P>



<PRE>
     # careful of extra block braces on fh ref
     print { $rec-&gt;{HANDLE} } &quot;a string\n&quot;;

</PRE>


<P>



<PRE>
     use FileHandle;
     $rec-&gt;{HANDLE}-&gt;<A HREF="../man1/autoflush.1.php">autoflush</A>(1);
     $rec-&gt;{HANDLE}-&gt;print(&quot; a string\n&quot;);

</PRE>


<A NAME="lbBB">&nbsp;</A>
<H2>Declaration of a <FONT SIZE="-1">HASH</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">COMPLEX</FONT> <FONT SIZE="-1">RECORDS</FONT></H2>

<A NAME="ixABG"></A>


<PRE>
     %TV = (
        flintstones =&gt; {
            series   =&gt; &quot;flintstones&quot;,
            nights   =&gt; [ qw(monday thursday friday) ],
            members  =&gt; [
                { name =&gt; &quot;fred&quot;,    role =&gt; &quot;lead&quot;, age  =&gt; 36, },
                { name =&gt; &quot;wilma&quot;,   role =&gt; &quot;wife&quot;, age  =&gt; 31, },
                { name =&gt; &quot;pebbles&quot;, role =&gt; &quot;kid&quot;,  age  =&gt;  4, },
            ],
        },

</PRE>


<P>



<PRE>
        jetsons     =&gt; {
            series   =&gt; &quot;jetsons&quot;,
            nights   =&gt; [ qw(wednesday saturday) ],
            members  =&gt; [
                { name =&gt; &quot;george&quot;,  role =&gt; &quot;lead&quot;, age  =&gt; 41, },
                { name =&gt; &quot;jane&quot;,    role =&gt; &quot;wife&quot;, age  =&gt; 39, },
                { name =&gt; &quot;elroy&quot;,   role =&gt; &quot;kid&quot;,  age  =&gt;  9, },
            ],
         },

</PRE>


<P>



<PRE>
        simpsons    =&gt; {
            series   =&gt; &quot;simpsons&quot;,
            nights   =&gt; [ qw(monday) ],
            members  =&gt; [
                { name =&gt; &quot;homer&quot;, role =&gt; &quot;lead&quot;, age  =&gt; 34, },
                { name =&gt; &quot;marge&quot;, role =&gt; &quot;wife&quot;, age =&gt; 37, },
                { name =&gt; &quot;bart&quot;,  role =&gt; &quot;kid&quot;,  age  =&gt;  11, },
            ],
         },
      );

</PRE>


<A NAME="lbBC">&nbsp;</A>
<H2>Generation of a <FONT SIZE="-1">HASH</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">COMPLEX</FONT> <FONT SIZE="-1">RECORDS</FONT></H2>

<A NAME="ixABH"></A>


<PRE>
     # reading from file
     # this is most easily done by having the file itself be
     # in the raw data format as shown above.  perl is happy
     # to parse complex data structures if declared as data, so
     # sometimes it's easiest to do that

</PRE>


<P>



<PRE>
     # here's a piece by piece build up
     $rec = {};
     $rec-&gt;{series} = &quot;flintstones&quot;;
     $rec-&gt;{nights} = [ find_days() ];

</PRE>


<P>



<PRE>
     @members = ();
     # assume this file in field=value syntax
     while (&lt;&gt;) {
         %fields = split /[\s=]+/;
         push @members, { %fields };
     }
     $rec-&gt;{members} = [ @members ];

</PRE>


<P>



<PRE>
     # now remember the whole thing
     $TV{ $rec-&gt;{series} } = $rec;

</PRE>


<P>



<PRE>
     ###########################################################
     # now, you might want to make interesting extra fields that
     # include pointers back into the same data structure so if
     # change one piece, it changes everywhere, like for example
     # if you wanted a {kids} field that was a reference
     # to an array of the kids' records without having duplicate
     # records and thus update problems.
     ###########################################################
     foreach $family (keys %TV) {
         $rec = $TV{$family}; # temp pointer
         @kids = ();
         for $person ( @{ $rec-&gt;{members} } ) {
             if ($person-&gt;{role} =~ /kid|son|daughter/) {
                 push @kids, $person;
             }
         }
         # REMEMBER: $rec and $TV{$family} point to same data!!
         $rec-&gt;{kids} = [ @kids ];
     }

</PRE>


<P>



<PRE>
     # you copied the array, but the array itself contains pointers
     # to uncopied objects. this means that if you make bart get
     # older via

</PRE>


<P>



<PRE>
     $TV{simpsons}{kids}[0]{age}++;

</PRE>


<P>



<PRE>
     # then this would also change in
     print $TV{simpsons}{members}[2]{age};

</PRE>


<P>



<PRE>
     # because $TV{simpsons}{kids}[0] and $TV{simpsons}{members}[2]
     # both point to the same underlying anonymous hash table

</PRE>


<P>



<PRE>
     # print the whole thing
     foreach $family ( keys %TV ) {
         print &quot;the $family&quot;;
         print &quot; is on during @{ $TV{$family}{nights} }\n&quot;;
         print &quot;its members are:\n&quot;;
         for $who ( @{ $TV{$family}{members} } ) {
             print &quot; $who-&gt;{name} ($who-&gt;{role}), age $who-&gt;{age}\n&quot;;
         }
         print &quot;it turns out that $TV{$family}{lead} has &quot;;
         print scalar ( @{ $TV{$family}{kids} } ), &quot; kids named &quot;;
         print join (&quot;, &quot;, map { $_-&gt;{name} } @{ $TV{$family}{kids} } );
         print &quot;\n&quot;;
     }

</PRE>


<A NAME="lbBD">&nbsp;</A>
<H2>Database Ties</H2>

<A NAME="ixABI"></A>
You cannot easily tie a multilevel data structure (such as a hash of
hashes) to a dbm file.  The first problem is that all but <FONT SIZE="-1">GDBM</FONT> and
Berkeley <FONT SIZE="-1">DB</FONT> have size limitations, but beyond that, you also have problems
with how references are to be represented on disk.  One experimental
module that does partially attempt to address this need is the <FONT SIZE="-1">MLDBM</FONT>
module.  Check your nearest <FONT SIZE="-1">CPAN</FONT> site as described in perlmodlib for
source code to <FONT SIZE="-1">MLDBM</FONT>.
<A NAME="lbBE">&nbsp;</A>
<H2>SEE ALSO</H2>

<A NAME="ixABJ"></A>
<I><A HREF="../man1/perlref.1.php">perlref</A></I>(1), <I><A HREF="../man1/perllol.1.php">perllol</A></I>(1), <I><A HREF="../man1/perldata.1.php">perldata</A></I>(1), <I><A HREF="../man1/perlobj.1.php">perlobj</A></I>(1)
<A NAME="lbBF">&nbsp;</A>
<H2>AUTHOR</H2>

<A NAME="ixABK"></A>
Tom Christiansen &lt;<I><A HREF="mailto:tchrist@perl.com">tchrist@perl.com</A></I>&gt;
<P>

Last update:
Wed Oct 23 04:57:50 <FONT SIZE="-1">MET</FONT> <FONT SIZE="-1">DST</FONT> 1996
<P>

<HR>
<A NAME="index">&nbsp;</A><H2>Index</H2>
<DL>
<DT><A HREF="#lbAB">NAME</A><DD>
<DT><A HREF="#lbAC">DESCRIPTION</A><DD>
<DT><A HREF="#lbAD">REFERENCES</A><DD>
<DT><A HREF="#lbAE">COMMON MISTAKES</A><DD>
<DT><A HREF="#lbAF">CAVEAT ON PRECEDENCE</A><DD>
<DT><A HREF="#lbAG">WHY YOU SHOULD ALWAYS use strict</A><DD>
<DT><A HREF="#lbAH">DEBUGGING</A><DD>
<DT><A HREF="#lbAI">CODE EXAMPLES</A><DD>
<DT><A HREF="#lbAJ">ARRAYS OF ARRAYS</A><DD>
<DT><A HREF="#lbAK">Declaration of an <FONT SIZE="-1">ARRAY</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">ARRAYS</FONT></A><DD>
<DT><A HREF="#lbAL">Generation of an <FONT SIZE="-1">ARRAY</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">ARRAYS</FONT></A><DD>
<DT><A HREF="#lbAM">Access and Printing of an <FONT SIZE="-1">ARRAY</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">ARRAYS</FONT></A><DD>
<DT><A HREF="#lbAN">HASHES OF ARRAYS</A><DD>
<DT><A HREF="#lbAO">Declaration of a <FONT SIZE="-1">HASH</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">ARRAYS</FONT></A><DD>
<DT><A HREF="#lbAP">Generation of a <FONT SIZE="-1">HASH</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">ARRAYS</FONT></A><DD>
<DT><A HREF="#lbAQ">Access and Printing of a <FONT SIZE="-1">HASH</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">ARRAYS</FONT></A><DD>
<DT><A HREF="#lbAR">ARRAYS OF HASHES</A><DD>
<DT><A HREF="#lbAS">Declaration of an <FONT SIZE="-1">ARRAY</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">HASHES</FONT></A><DD>
<DT><A HREF="#lbAT">Generation of an <FONT SIZE="-1">ARRAY</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">HASHES</FONT></A><DD>
<DT><A HREF="#lbAU">Access and Printing of an <FONT SIZE="-1">ARRAY</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">HASHES</FONT></A><DD>
<DT><A HREF="#lbAV">HASHES OF HASHES</A><DD>
<DT><A HREF="#lbAW">Declaration of a <FONT SIZE="-1">HASH</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">HASHES</FONT></A><DD>
<DT><A HREF="#lbAX">Generation of a <FONT SIZE="-1">HASH</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">HASHES</FONT></A><DD>
<DT><A HREF="#lbAY">Access and Printing of a <FONT SIZE="-1">HASH</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">HASHES</FONT></A><DD>
<DT><A HREF="#lbAZ">MORE ELABORATE RECORDS</A><DD>
<DT><A HREF="#lbBA">Declaration of <FONT SIZE="-1">MORE</FONT> <FONT SIZE="-1">ELABORATE</FONT> <FONT SIZE="-1">RECORDS</FONT></A><DD>
<DT><A HREF="#lbBB">Declaration of a <FONT SIZE="-1">HASH</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">COMPLEX</FONT> <FONT SIZE="-1">RECORDS</FONT></A><DD>
<DT><A HREF="#lbBC">Generation of a <FONT SIZE="-1">HASH</FONT> <FONT SIZE="-1">OF</FONT> <FONT SIZE="-1">COMPLEX</FONT> <FONT SIZE="-1">RECORDS</FONT></A><DD>
<DT><A HREF="#lbBD">Database Ties</A><DD>
<DT><A HREF="#lbBE">SEE ALSO</A><DD>
<DT><A HREF="#lbBF">AUTHOR</A><DD>
</DL>

</div>






</div>




</body>
</html>
